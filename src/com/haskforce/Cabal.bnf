{
  parserClass="com.haskforce.cabal.CabalParser"
  implements="com.haskforce.cabal.psi.CabalCompositeElement"
  extends="com.haskforce.cabal.psi.impl.CabalCompositeElementImpl"
  elementTypeHolderClass="com.haskforce.cabal.psi.CabalTypes"
  elementTypeClass="com.haskforce.cabal.psi.CabalElementType"
  tokenTypeClass="com.haskforce.cabal.psi.CabalTokenType"
  psiClassPrefix="Cabal"
  psiImplClassSuffix="Impl"
  psiPackage="com.haskforce.cabal.psi"
  psiImplPackage="com.haskforce.cabal.psi.impl"
  tokens=[
    comma = ','
    colon= ':'
    dot = "."
    slash ="/"
    at ="@"
    eq = "=="
    gt = ">"
    lt = "<"
    gtEq = ">="
    ltEq = "<="
    and = "&&"
    true = "true"
    false = "false"
    nameKey = "name"
    versionKey = "version"
    synopsisKey = "synopsis"
    licenseKey = "license"
    categoryKey = "category"
    buildTypeKey = "build-type"
    licenseFileKey ="license-file"
    homepageKey ="homepage"
    buildableKey = "buildable"
    descriptionKey = "description"
    bugReportsKey ="bug-reports"
    packageKey ="package"
    typeKey = "type"
    testModuleKey = "test-module"
    licenseFilesKey ="license-files"
    stabilityKey ="stability"
    dataDirKey ="data-dir"
    copyrightKey ="copyright"
    authorKey ="author"
    dataFilesKey ="data-files"
    testedWithKey ="tested-with"
    maintainerKey ="maintainer"
    cabalVersionKey = "cabal-version"
    defaultLanguageKey = "default-language"
    extraSourceFilesKey = "extra-source-files"
    extraDocFilesKey = "extra-doc-files"
    extraTmpFilesKey = "extra-tmp-files"
    buildDependsKey = "build-depends"
    otherExtensionsKey = "other-extensions"
    otherModulesKey = "other-modules"
    exposedModulesKey = "exposed-modules"
    exposedKey = "exposed"
    hsSourceDirsKey = "hs-source-dirs"
    extensionsKey = "extensions"
    ghcOptionsKey = "ghc-options"
    ghcProfOptionsKey = "ghc-prof-options"
    ghcSharedOptionsKey = "ghc-shared-options"
    buildToolsKey = "build-tools"
    includesKey = "includes"
    mainIsKey = "main-is"
    installIncludesKey = "install-includes"
    includeDirsKey = "include-dirs"
    cSourcesKey = "c-sources"
    jsSourcesKey = "js-sources"
    extraLibrariesKey = "extra-libraries"
    extraGhciLibrariesKey = "extra-ghci-libraries"
    extraLibDirsKey = "extra-lib-dirs"
    ccOptionsKey = "cc-options"
    ldOptionsKey = "ld-options"
    pkgConfigDependsKey = "pkg-config-depends"
    frameworksKey = "frameworks"
    commentRegexp='regexp:--([^\^\r\n][^\r\n]*|[\r\n])'
    varidRegexp="regexp:[a-zA-Z_\-0-9']*"
    filePathRegexp="regexp:[a-zA-Z_\-0-9*./']*"
    numberRegexp="regexp:[0-9]+"
    freeformRegexp="regexp:[^]*"
    crlf='regexp:([\r\n])'
    WHITESPACELBRACETOK="Synthetic leftbrace"
    WHITESPACERBRACETOK="Synthetic rightbrace"
  ]
}

cabal ::= keyOrConfig* | comment

comment ::= commentRegexp

conditional ::= "if"

library ::= "library" open libraryKeys+ close

libraryKeys ::= librarySpecificKeys | buildInformation

executableSpecificKeys ::= mainIsKey colon open fileName close

executableKeys ::= executableSpecificKeys | buildInformation

librarySpecificKeys ::= exposedModulesKey colon open <<commaSeparate module >> close |
                        exposedModulesKey colon open module* close |
                        exposedKey colon open bool close

executable ::= "executable" varid open executableKeys+ close

testSuite ::= "test-suite" varid open testSuiteKeys+ close

testSuiteKeys ::= testSuiteSpecificKeys | buildInformation

testSuiteSpecificKeys ::= typeKey colon open testInterface close | mainIsKey colon open fileName close | testModuleKey colon open varid close

config ::= (library | executable | testSuite )

keyOrConfig ::= key | config

varid ::= varidRegexp

dependencyName ::= varidRegexp

buildType ::= "simple" | "configure" | "make" | "custom"

testInterface ::= "exitcode-stdio-1.0" | "detailed-0.9"

// Should we get these regexes finer grained? Now they match nearly anything I guess.
fileName ::= varid "." varid

filePath ::= filePathRegexp

address ::= freeformRegexp*

url ::= freeformRegexp*

freeform ::= freeformRegexp*

directory ::= <<slashSeparate varid>>

compiler ::= varidRegexp versionConstraint version

bool ::= true | false

key ::= nameKey colon open varid close|
        synopsisKey colon open freeform close|
        licenseKey colon open varid close|
        authorKey colon open freeform close|
        categoryKey colon open varid close|
        copyrightKey colon open freeform close|
        maintainerKey colon open address close|
        buildTypeKey colon open buildType close|
        stabilityKey colon open varid close|
        homepageKey colon open url close |
        bugReportsKey colon open url close|
        packageKey colon open url close|
        cabalVersionKey colon open gtEq version close|
        licenseFileKey colon open freeform close|
        descriptionKey colon open freeform close|
        dataDirKey colon open directory close|
        versionKey colon open version close|
        extraSourceFilesKey colon open (<<commaSeparate varid >> | filePath*) close |
        extraDocFilesKey colon open <<commaSeparate varid>> close |
        extraTmpFilesKey colon open <<commaSeparate varid>> close |
        otherExtensionsKey colon open <<commaSeparate varid>> close |
        testedWithKey colon open <<commaSeparate compiler>> close |
        dataFilesKey colon open <<commaSeparate fileName>> close |
        licenseFilesKey colon open <<commaSeparate fileName>> close |



buildInformation ::= buildDependsKey colon open <<commaSeparate dependency>> close |
                     otherModulesKey colon open <<cabalList module >> close|
                     hsSourceDirsKey colon open <<cabalList directory>> close|
                     extensionsKey colon open <<commaSeparate varid>> close |
                     buildToolsKey colon open <<commaSeparate dependency>> close|
                     buildableKey colon open bool close|
                     ghcOptionsKey colon open <<dashSeparate varid>> close|
                     ghcProfOptionsKey colon open <<dashSeparate varid>> close |
                     ghcSharedOptionsKey colon open  <<dashSeparate varid>> close |
                     includesKey colon open <<commaSeparate fileName>> close |
                     installIncludesKey colon open <<commaSeparate fileName>> close |
                     includeDirsKey colon open <<commaSeparate directory>> close |
                     cSourcesKey colon open <<commaSeparate fileName>> close |
                     jsSourcesKey colon open <<commaSeparate fileName>> close |
                     extraLibrariesKey colon open <<commaSeparate varid>> close |
                     extraGhciLibrariesKey colon open <<commaSeparate varid>> close |
                     extraLibDirsKey colon open <<commaSeparate directory>> close |
                     ccOptionsKey colon open <<commaSeparate varid>> close |
                     ldOptionsKey colon open <<commaSeparate varid>> close |
                     pkgConfigDependsKey colon open <<commaSeparate dependency>> close |
                     frameworksKey colon open <<commaSeparate varid>> close |
                     defaultLanguageKey colon open varid close|

private meta commaSeparate ::= <<p>> (',' <<p>>)* {pin(".*")=1}

//TODO find out how pinning works in this case.
// Or maybe just find out how pinning actually works.
private meta cabalList ::= <<p>> (',' <<p>>)+ | <<p>>+

private meta dotSeparate ::= <<p>> ('.' <<p>>)* {pin(".*")=1}

private meta dashSeparate ::= <<p>> ('-' <<p>>)* {pin(".*")=1}

private meta slashSeparate ::= <<p>> ('/' <<p>>)* {pin(".*")=1}


module ::= <<dotSeparate varid>>

number ::= numberRegexp

version ::= <<dotSeparate number>>

versionConstraint ::= eq | gt | lt | gtEq | ltEq

dependency ::= dependencyName [versionConstraint version [and versionConstraint version]]

private open ::= WHITESPACELBRACETOK

private close ::= WHITESPACERBRACETOK
